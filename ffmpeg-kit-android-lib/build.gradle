apply plugin: 'com.android.library'

android {
    compileSdkVersion 31
    ndkVersion "22.0.7026061"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 240440
        versionName "4.4"
        project.archivesBaseName = "ffmpeg-kit"
        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ["../libs"]
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

task javadoc(type: Javadoc) {
    title = 'FFmpegKit'
    destinationDir = file("${projectDir}/../../docs/android/javadoc")
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    source = android.sourceSets.main.java.srcDirs
    configurations.implementation.setCanBeResolved(true)
    classpath += configurations.implementation
}

dependencies {
    implementation 'com.arthenica:smart-exception-java:0.1.0'
    testImplementation "androidx.test.ext:junit:1.1.2"
    testImplementation "org.json:json:20201115"
}

if (System.properties.containsKey('releaseFFmpegKit')) {
    apply from: "${rootProject.projectDir}/../tools/release/android/release.template.gradle"
}
